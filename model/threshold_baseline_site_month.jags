
    model {
    
    for (x in 1:Nobs){
    
    #Observation of a flowering plant
    Y[x] ~ dbern(p[x])
    logit(p[x]) <- alpha[Plant[x],Site[x],Month[x]] 
    
    #Residuals
    discrepancy[x] <- abs(Y[x] - p[x])
    
    #Assess Model Fit
    Ynew[x] ~ dbern(p[x])
    discrepancy.new[x]<-abs(Ynew[x] - p[x])
    }
    
    
    #Sum discrepancy
    fit<-sum(discrepancy)/Nobs
    fitnew<-sum(discrepancy.new)/Nobs
    
    #Prediction
    
    for(x in 1:Npreds){
    #Observation - probability of flowering
    prediction[x] ~ dbern(p_new[x])
    logit(p_new[x])<-alpha[NewPlant[x],NewSite[x],NewMonth[x]]
    
    #predictive error
    pred_error[x] <- abs(Ypred[x] - p_new[x])
    }
    
    
    #Predictive Error
    fitpred<-sum(pred_error)/Npreds
    
    #Priors
    
    #Species level priors
    for (i in 1:Plants){
      sigma_month[i] ~ dgamma(0.0001,0.0001)
    for(j in 1:Sites){
      alpha_site[i,j] ~ dnorm(0,0.386)
    for(k in 1:Months){
    #Intercept flowering probability
      alpha[i,j,k] ~ dnorm(alpha_site[i,j],sigma_month[i])
    }
    } 
    }
    }
    
