
    model {

    # Bird presence at each camera (placed on a single flower Plant (j))
    for (i in 1:Birds){
      for( j in 1:Plants){
        for(k in 1:Cameras){
          #Latent Interaction State for each camera
          z[i,j,k]~dbern(alpha[i,j])
              } 
          }
      }
    
    #Observed interactions
    for (x in 1:Nobs){

      #Probability of detection, conditional on interaction
      psi[x] <- omega[Bird[x]] * z[Bird[x],Plant[x],Camera[x]]
      Yobs[x] ~ dbern(psi[x])
    }
    
    #Priors
    
    #Species level priors
    for (i in 1:Birds){
      for (j in 1:Plants){

        #Flat interaction intensity prior
        alpha[i,j] ~ dbeta(1,1)
      } 
      }
    
    #Observation model
    #Detect priors, logit transformed - Following lunn 2012 p85
    for(x in 1:Birds){
    logit(omega[x])<-dcam[x]
    dcam[x] ~ dnorm(omega_mu,omega_tau)
    }
    
    omega_mu ~ dnorm(0,0.386)
    omega_tau <- pow(omega_sigma,-2)
    omega_sigma ~ dunif(0,0.25)
    }
    
