
    model {
    
    for (x in 1:Nobs){

      #Effect of environment
      mu[x] <- alpha[Plant[x]] + beta[Plant[x]] * ele[x]

      #observation
      Yobs[x] ~ dnorm(mu[x],tau[Plant[x]])T(0,365)
      
      #Residuals
      residuals[x] <- Yobs[x] - mu[x]
      
      #squared error
      sq[x]<-pow(residuals[x],2)

      #Assess Model Fit - squared residuals
      Ynew[x] ~ dnorm(mu[x],tau[Plant[x]])T(0,365)
      sq.new[x]<-pow(Ynew[x] - mu[x],2)
    
    }
    
    #Root mean squared residuals
    fit<-sqrt(sum(sq[])/Nobs)
    fitnew<-sqrt(sum(sq.new[])/Nobs)  

    #Prediction

    for(i in 1:Npreds){

      #predict value
      mu_new[i]<-alpha[Ypred_plant[i]] + beta[Ypred_plant[i]] * ele_new[Ypred_plant[i]]
      prediction[i] ~ dnorm(mu_new[i],tau[Ypred_plant[i]])T(0,365)
    
      #squared predictive error
      pred_error[i] <- pow(Ypred[i] - prediction[i],2)
    }

    #Root Mean Squared Predictive Error
    fitpred<-sqrt(sum(pred_error[])/Npreds)

    #Priors
    
    #Species level priors
    
    for (j in 1:Plants){
    
    #Intercept
    alpha[j] ~ dnorm(0,0.0001)
  
    #Effect of elevation
    beta[j] ~ dnorm(0,0.0001)

    #variance
    sigma[j] ~ dunif(0,75)
    tau[j] <- pow(sigma[j], -2)
    } 
    
    }
    
